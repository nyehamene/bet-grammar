literals                = @import("literals.grammar");
// Style
Style                   = "style" rule sep;
rule                    = style_block | at_rule;
// StyleRule
style_block             = "{" ( comment | style_rule ) "}";
style_rule              = decl ";";
decl                    = property
                        | at_rule
                        | style_nesting
                        ;
style_nesting           = ( selector_list | percentage ) style_block;
// AtRule
at_rule                 = "@" ident { prelude } [ style_block ];
prelude                 = prelude_value;
prelude_value           = basic_expr
                        | paren_prelude_value
                        | prelude_list
                        | paren_decl
                        ;
paren_prelude_value     = "(" prelude_value ")";
prelude_list            = prelude_value { [ "," ] prelude_value };
// Selector
selector_list           = selector { "," selector };
selector                = simple_selector { combinator simple_selector };
simple_selector         = type_selector
                        | class_selector
                        | id_selector
                        | attr_selector
                        | pseudo_class_selector
                        | pseudo_element_selector
                        | nesting_selector
                        ;
type_selector           = ident;
class_selector          = literals.ident_d;
id_selector             = "#" ident;
nesting_selector        = "&";
attr_selector           = "[" attr_selector_value "]";
attr_selector_value     = ident attr_selector_op basic_expr [ attr_selector_mod ]
                        | ident
                        ;
attr_selector_op        = [ ( "~" | "|" | "^" | "$" | "*" ) ] "=";
attr_selector_mod       = "i" | "I" | "s" | "S";
pseudo_class_selector   = ":" ident [ "(" [ selector_list ] ")" ];
pseudo_element_selector = "::" ident [ "(" [ selector_list ] ")" ];
combinator              = ( ">" | "+" | "!" | "~" | " " );
//
property                = ident ":" expr;
paren_decl              = "(" decl { ( "," | ";" ) decl };
expr                    = basic_expr
                        | color
                        | calc
                        | list
                        | url
                        | paren_expr
                        | function_call
                        ;
basic_expr              = string
                        | ident
                        | number
                        | size
                        | percentage
                        | variable
                        ;
calc                    = "calc" "(" calc_expr ")";
calc_expr               = basic_expr | binary_expr | unary_expr;
url                     = "url" "(" ( string | variable ) ")";
variable                = "var" "(" ident ")";
function_call           = ident "(" [ expr ] ")";
binary_expr             = basic_expr { ( "+" | "-" | "*" | "/" ) basic_expr };
unary_expr              = ( "+" | "-" ) expr;
paren_expr              = "(" expr ")";
// List
list                    = expr { [ "," ] expr };
// Color
color                   = color_hex
                        | color_rgb
                        | color_hsl
                        | current_color
                        ;
color_hsl               = hsl "(" expr ")";
color_rgb               = rgb "(" expr ")";
color_hex               = "#" { literals.digit_hex };
// Number
percentage              = number "%";
size                    = number ident;
number                  = literals.number_dec;
string                  = literals.string;
// Ident
ident                   = /[a-zA-Z_-][a-zA-Z0-9_-]*/;
// Keyword
current_color           = "currentcolor";
hsl                     = "hsl" | "hsla";
rgb                     = "rgb" | "rgba";
// Comment
// Tree-sitter: use an external scanner to scan comment
comment                 = "/*" /[\*]*/ { comment_content } /[\*]*/ "*/";
comment_content         = /[^\*]*[^\*]/;
sep                     = ";" | "\n";
